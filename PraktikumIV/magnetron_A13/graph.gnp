
#####Kalibracia
reset
set fit errorvariables
set fit quiet
set samples 1000
set print "output/gnuplot_output.dat"
#~ set border linewidth 1.5

set term epslatex color colortext size  7, 5
set output 'output/spolu1.tex'

set key at graph 0.27, 0.98

set xrange [-30:200]
set yrange [-0.00003:0.001]

set xlabel  'Napätie $U\,/\unit{V}$'
set ylabel  'Prúd magnetrónom $I\,/\unit{A}$'
set style line 1 lc rgb 'black' lt 1 lw 2

load 'output/spolu.gnp'
#~ quit
set term epslatex monochrome size 6, 4
set output 'output/naboj_graph.tex'
set xrange [*:*]
set yrange [*:*]

f(x) = a*x**2

fit f(x) 'output/napatia_p.dat' using 1:3 via a

set key at graph 0.5, 0.9
set xlabel '$B\di{kr} \,/\unit{T}$'
set ylabel '$U\di{kr} \,/\unit{V}$'
tx = 0.5
ty = 0.7
spacing = 0.06

print 'a ', a, a_err


set label 1 sprintf('$\alpha = (%1.0f\pm %1.0f)\unit{kV\cdot T^{-2}}$', a*1e-3, a_err*1e-3) at graph tx, ty right

plot 'output/napatia_p.dat' using 1:3:2:4 lw 5 ps 3 with xyerrorbars title 'Namerané hodnoty $U\di{kr}(B\di{kr})$', f(x) lw 3 title 'Závislosť $U\di{kr} = \alpha B\di{kr}^2$'

set output 'output/chosen_graph.tex'
set xrange [*:*]
set yrange [-0.00001:0.0008]

unset label
set key at graph 0.97, 0.9
set xlabel '$B \,/\unit{T}$'
set ylabel '$I\di A \,/\unit{A}$'

f(x) = b/(1+exp((x-x0)*a))
a=1000
b = 0.0008
x0 = 0.01
fit f(x) 'output/chosen.dat' using 1:3 via a, b, x0

plot 'output/chosen.dat' using 1:3  ps 3 lw 5 title 'Namerané $I\di A(B)$', f(x) title 'Vodítko pre oko'
